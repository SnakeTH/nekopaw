*preinstalled=0

[main]

$main.url='http://comicartcommunity.com/gallery/';
$main.icon='comicartcommunity.com.bmp';
$main.short = 'caco';
$main.cheatsheet = 'http://comicartcommunity.com/gallery/search.php';
$main.encoding = 'ASCII';

;$picture.template.ext = '%ext%';

@addfield("new","New only","checkbox","FALSE");
@addfield("username","Username","textedit","");
@addfield("term","Search term","indexlistbox",0,"OR","AND");
@addfield("fields","Fields","indexlistbox",0,"all fields","only image name","only description","only keywords");

$main.loginscript = "login";

$picture.fields(url,id,preview,post_url,category);

$userstring = 'user_name';
$pwdstring = 'user_password';
$main.login = '$main.url+"login.php?"+$userstring+"="+$login+"&"+$pwdstring+"="+$password + "&__csrf=" + $csrf';

$gui_thumbs_part = '/thumbnails/';
$gui_images_part = '/media/';
$picsperpage = 15;

[login]

$thread.loginresult = 1;
$c = @cookie("4images_userid");
$csrf = '';

?(@isempty($login)=0)&((@isempty($c)!0)|($c=-1)){
	$thread.url = "'" + $main.url + "index.php'";
	$thread.xmlcontent = "html";
	$thread.xml = "loginscript";
	@thread.execute;
	
	$thread.referer = $main.url + "index.php";
	$thread.url = $main.login;
	$thread.method = "post";
	$thread.onerror = 'onerror';
	@thread.execute;
	?@cookie("4images_userid") = -1 {
		$thread.loginresult = 0;
	}
}

[loginscript]

^input#name='__csrf'{
	$csrf=#value;
}

[onerror]

?$thread.http.code = 403{$thread.accepterror = 0;}

[dwscript]

$thread.url = $picture%url;
$thread.referer = $picture%post_url;

[listscript]

;$thread.tryext = "jpg,gif,png";
$thread.xmlcontent = 'html';
$thread.method = 'get';
$thread.url = "'" + $main.url + "search.php'";
$thread.xml = 'loginscript';

@thread.execute;

$thread.method = 'post';
$thread.xml = 'gui';
$url = "search.php?__csrf=" + $csrf + "&search_keywords=" + @httpencode($tag) + "&cat_id=0&search_user=" + @httpencode($username);

;search fields
?$fields=0{$url = $url + "&search_fields=all";}
?$fields=1{$url = $url + "&search_fields=image_name";}
?$fields=2{$url = $url + "&search_fields=image_description";}
?$fields=3{$url = $url + "&search_fields=image_keywords";}

;search term
?$term=0{$url = $url + "&search_terms=any";}
?$term=1{$url = $url + "&search_terms=all";}

$thread.url = "'" + $main.url + $url + "'";
@thread.execute;

$thread.url = "'" + $main.url + $url + "search.php?show_result=1&page=' + $thread.counter + 1";
$thread.method = 'get';

[gui]

^tr#class="imagerow1"{
	^td{
		^a{
			$post_url = $main.url + @deleteto("/",#href);
			^img{
				@addpicture(
					%url = $main.url + @deleteto("/",@replace($gui_images_part,$gui_thumbs_part,#src)),
					%post_url = $post_url,
					%preview = $main.url + @deleteto("/",#src),
					%id = @urlvar("image_id",$post_url)
				);
				$picture.filename = @emptyname($picture%url);
			}
			?@isempty(@urlvar("cat_id",#href))=0{%category=@text;}
		}
	}
}

^tr#class="imagerow2"{
	^td{
		^a{
			$post_url = $main.url + @deleteto("/",#href);
			^img{
				@addpicture(
					%url = $main.url + @deleteto("/",@replace($gui_images_part,$gui_thumbs_part,#src)),
					%post_url = $post_url,
					%preview = $main.url + @deleteto("/",#src),
					%id = @urlvar("image_id",$post_url)
				);
				$picture.filename = @emptyname($picture%url);
			}
			?@isempty(@urlvar("cat_id",#href))=0{%category=@text;}
		}
	}
}

^a#class="paging"{
	?$thread.count < @ifempty(@urlvar("page",#href),1){
		$thread.count = @ifempty(@urlvar("page",#href),0);
		$thread.result = $thread.count * $picsperpage;
	}
}