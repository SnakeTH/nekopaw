*preinstalled=0

[main]
$main.url = 'https://www.furaffinity.net/';
$main.icon = 'furaffinity.net.bmp';
$picture.fields(preview,post_url,title,author);
$main.short = 'fura';

$main.checkcookie = 'a';
$userstring = 'name';
$pwdstring = 'pass';
$main.login = "$main.url + 'login/?ref=https://www.furaffinity.net/'";
$main.loginpost = "$userstring + '=' + $login + '&' + $pwdstring + '=' + $password + '&action=login&retard_protection=1&login=Login+to&nbsp;FurAffinity'";
$main.pagebypage = 1;
$picture.template.name = '%filename%';
$picture.template.ext = '%ext%';
$proto = 'https:';

@addfield("mode","Search Mode","indexlistbox",0,"by tags","by %username% pictures","by %username% favorites");
@addfield("art","Art","checkbox",1);
@addfield("flash","Flash","checkbox","False");
@addfield("photo","Photography","checkbox","False");
@addfield("music","Music","checkbox","False");
@addfield("story","Story","checkbox","False");
@addfield("poetry","Poetry","checkbox","False");

$picsperpage = 60;

[listscript]

$thread.xmlcontent = 'html';
$thread.method = 'post';

?$mode=0{

$s = "'" + $main.url + "search/?q=" + @isolate("'",$tag) + 
"&perpage=60" +
"&order-by=date" +
"&order-direction=desc" +
"&range=all" +
"&rating-general=on";

?$art = "True"{$s = $s + "&type-art=on";}
?$flash = "True"{$s = $s + "&type-flash=on";}
?$photo = "True"{$s = $s + "&type-photo=on";}
?$music = "True"{$s = $s + "&type-music=on";}
?$story = "True"{$s = $s + "&type-story=on";}
?$poetry = "True"{$s = $s + "&type-poetry=on";}

$s = $s + "&mode=extended";

$thread.url = $s + "&page=' + $thread.counter + 1 + '&next_page=&gt;&gt;&gt; 60 more &gt;&gt;&gt;'";

$thread.xml = 'xmlscript';

}

?$mode=1{

	$thread.url = "'" + $main.url + "gallery/" + @httpencode($tag) + "/' + $thread.counter + 1 + '/?perpage=60'";
	$thread.xml = 'xmlgallery';	
}

?$mode=2{

	$thread.url = "'" + $main.url + "favorites/" + @httpencode($tag) + "/' + $thread.counter + 1 + '/?perpage=60'";
	$thread.xml = 'xmlgallery';	
}

@thread.execute;

[dwscript]

$thread.referer = $picture%post_url;
$thread.method = 'get';
$thread.xml = 'xmlpicture';
$thread.url = '"' + $picture%post_url + '"';
@thread.execute;

[xmlpicture]

$picture.filename = '';
$thread.url = '';

^div#class='actions'{
	^a{
		?@trim(@text)='Download'{
			$fname = @copybacktoex("/",#href);
			$thread.url = $proto + @replace("/","/" + $fname,#href) + @httpencode($fname);
			$picture.filename=@emptyname($thread.url);
		}
	}
}

[xmlscript]

^div#id='search-results'{
	^input#name='next_page'#class!'hidden'{
		$thread.count = $thread.counter + 2;
	}

	^{
	^u{
		^a{
			$href = #href;
			^img{
				addpicture(%post_url = $main.url+@trim($href,"/"),
					%preview = $proto + #src
				);
				$picture.filename = '%filename%.%ext%';
			}
		}
	}
	^span{%title = @text;}
	^small{^a{%author = @text;}}
	}
}

[xmlgallery]

$nothing = 1;

^td#class='alt1'{
	^{
	^u{
		^a{
			$href = #href;
			^img{
				addpicture(%post_url = $main.url+@trim($href,"/"),
					%preview = $proto + #src,
					%author = $tag
				);
				$picture.filename = '%filename%.%ext%';
				$nothing = 0;
			}
		}
	}
	^span{%title = @text;}
	^small{^a{%author = @text;}}
	}	
}

?$nothing = 0{
	$thread.count = $thread.counter + 2;
}