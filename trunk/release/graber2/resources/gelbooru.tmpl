*preinstalled=0

;template for gelbooru engine

[main]
$main.cheatsheet = "http://gelbooru.com/index.php?page=help&topic=cheatsheet";

$picture.fields(url,preview,sample_url,post_url,width:i,height:i,rating,id:i,posted:d,md5,score:i,source,aspect_ratio:f,album);

@addfield("src","Source","indexlistbox",0,"Posts","Pool ID");

$main.checkcookie = 'user_id';
$userstring = 'user';
$pwdstring = 'pass';
$main.login = '$main.url+"index.php?page=account&s=login&code=00"';
$main.loginpost = '$userstring+"="+$login+"&"+$pwdstring+"="+$password+"&submit=Log in"';

$source = 'api';
$picsperpage = 28;
$gui_thumbs_dir = 'thumbs/';
$gui_images_dir = 'images/';
$gui_thumbs_part = 'thumbnail_';
$gui_images_part = '';


[listscript]

?$src = 0{
	?$source = 'api'{
		$thread.url = "$main.url+'index.php?page=dapi&s=post&q=index&tags='+@httpencode($tag)";
		$thread.xml = 'firstpagexml';
		$thread.execute();
		$thread.xml = 'listxml';
		$thread.url = "$main.url+'index.php?page=dapi&s=post&q=index&tags='+@httpencode($tag)+'&pid='+$thread.counter";
	}
	?$source = 'gui'{
		$thread.xmlcontent = 'html';
		$thread.url = "$main.url+'index.php?page=post&s=list&tags='+@httpencode($tag)";
		$thread.xml = 'fpgui';
		$thread.execute();
		$thread.xml = 'listgui';
		$thread.url = "$main.url+'index.php?page=post&s=list&tags='+@httpencode($tag)+'&pid='+$thread.counter*$picsperpage";	
	}
}

?$src = 1{
	;http://gelbooru.com/index.php?page=pool&s=show&id=10902
	$thread.url = "$main.url+'index.php?page=pool&s=show&id='+@httpencode($tag)";
	$thread.xml = 'listgui';
	$thread.execute();
}

[dwscript]
$thread.url = $picture%url;
$thread.referer = $picture%post_url;

[firstpagexml]
^posts{
	$thread.count=@min(1000,#count/100+1);
	$thread.result=#count;
	^post{
		addpicture(
			%url=#file_url,
			%preview=#preview_url,
			%sample_url=#sample_url,
			%post_url=$main.url+'index.php?page=post&s=view&id='+#id,
			%width=#width,
			%height=#height,
			%rating=#rating,
			%id=#id,
			%posted=@vartime("en-US",#created_at,"ddd mmm dd hh:nn:ss xxxxx yyyy"),
			%md5=#md5,
			%source=#source,
			%aspect_ratio=#width/#height,
			%tags=csv(#tags," ")
		);
		$picture.filename = @emptyname(#file_url);
	}
}

[listxml]
^posts{
	^post{
		addpicture(
			%url=#file_url,
			%preview=#preview_url,
			%sample_url=#sample_url,
			%post_url=$main.url+'index.php?page=post&s=view&id='+#id,
			%width=#width,
			%height=#height,
			%rating=#rating,
			%id=#id,
			%posted=@vartime("en-US",#created_at,"ddd mmm dd hh:nn:ss xxxxx yyyy"),
			%md5=#md5,
			%source=#source,
			%aspect_ratio=#width/#height,
			%tags=csv(#tags," ")
		);
		$picture.filename = @emptyname(#file_url);
	}
}

[fpgui]
^span#class='thumb'{
	^a{
		$post_url = #href;
	}
	^img{
		addpicture(
			%url=@replace($gui_images_part,$gui_thumbs_part,@replace($gui_images_dir,$gui_thumbs_dir,@removevars(#src))),
			%preview=#src,
			%post_url=$main.url+@trim($post_url,"/"),
			%rating=@trim(@copyfromtoex(" ","rating:",#title + " ")),
			%score=@ifempty(@trim(@copyfromtoex(" ","score:",#title + " ")),0),
			%id=@ifempty(@urlvar("id",$post_url),0),
			%tags=csv(@trim(@copyto("score:",#title))," ")
		);
		$picture.filename = @emptyname($picture%url);
	}
}
^div#id='paginator'{
	^a#alt='last page'{
		$thread.count=@min(1000,@ifempty(@urlvar("pid",#href),0)/$picsperpage + 1);
		$thread.result=($thread.count)*$picsperpage;
	}
	^a#name='last_page'{
		$thread.count=@min(1000,@ifempty(@urlvar("pid",#href),0)/$picsperpage + 1);
		$thread.result=($thread.count)*$picsperpage;
	}
}

[listgui]
^h3{
	$pool_name = @copyfrom("Now Viewing: ",@text);
}
^span#class='thumb'{
	^a{
		$post_url = #href;
	}
	^img{
		$url = @replace($gui_images_part,$gui_thumbs_part,@replace($gui_images_dir,$gui_thumbs_dir,@removevars(#src)));
		$url = @replace($gui_images_dir,$gui_images_dir + "/",$url);
		addpicture(
			%url=$url,
			%preview=#src,
			%post_url=$main.url+@trim($post_url,"/"),
			%rating=@trim(@copyfromtoex(" ","rating:",#title + " ")),
			%score=@trim(@copyfromtoex(" ","score:",#title + " ")),
			%id=@urlvar("id",$post_url),
			%tags=csv(@trim(@copyto("score:",#title))," ")
		);
		?$src = 1{ %album = $pool_name; }
		$picture.filename = @emptyname($picture%url);
	}
}